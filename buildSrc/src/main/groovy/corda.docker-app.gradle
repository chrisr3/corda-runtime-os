plugins {
    id 'corda.common-app'
}

configurations {
    dockerImage {
        canBeConsumed = false
        transitive = false
    }

    dockerImageJdbc {
        canBeConsumed = false
        transitive = false
    }

    profilerAgent {
        canBeConsumed = false
        visible = false
    }
}

if (enableProfiling.toBoolean()) {
    dependencies {
        // Unzip the Java profiler into the Docker image.
        profilerAgent "com.yourkit.corda:yourkit-agent-linux-x86-64:$profilerVersion@zip"
        dockerImage provider { zipTree(configurations.profilerAgent.singleFile) }
    }
}

// Proposed alternative to publishOSGiImage
// Currently in trial phase, being rolled out in Nightly and locally and planned to be deployed in phases down the line
tasks.register('publishBuildkitImage', BuildkitBuild) {
    it.sourceFiles.setFrom(tasks.named('appJar', Jar))
    it.extraSourceFiles.setFrom(configurations.dockerImage)
    it.jdbcDriverFiles.setFrom(configurations.dockerImageJdbc)
    it.useShortName = true
    it.containerName = project.name

    if (project.hasProperty('baseImage')) {
        baseImageName = baseImage
    }

    if (project.hasProperty('useBuildx')) {
        isBuildx = useBuildx.toBoolean()
    }

    if (project.hasProperty('tag')) {
        containerTag = tag
    }

    if (project.hasProperty('appendTag')) {
        appendContainerTag = appendTag
    }

    if (project.hasProperty('repo')) {
        containerRepo = repo
    }

    if (project.hasProperty('isNightly')) {
        nightlyBuild = isNightly.toBoolean()
    }

    if (project.hasProperty('isPreTest')) {
        preTest = isPreTest.toBoolean()
    }

    if (project.hasProperty('isReleaseCandidate')) {
        releaseCandidate = isReleaseCandidate.toBoolean()
    }

    if (project.hasProperty('publishToDockerDaemon')) {
        useDockerDaemon = publishToDockerDaemon.toBoolean()
    }
}

tasks.register('publishOSGiImage', DeployableContainerBuilder) {
    it.sourceFiles.setFrom(tasks.named('appJar', Jar))
    it.extraSourceFiles.setFrom(configurations.dockerImage)
    it.jdbcDriverFiles.setFrom(configurations.dockerImageJdbc)
    it.useShortName = true

    if (project.hasProperty('jibRemotePublish')) {
        remotePublish = jibRemotePublish.toBoolean()
    }

    if (project.hasProperty('doDockerHubPublish')) {
        dockerHubPublish = doDockerHubPublish.toBoolean()
    }

    if (project.hasProperty('isReleaseCandidate')) {
        releaseCandidate = isReleaseCandidate.toBoolean()
    }

    if (project.hasProperty('isNightly')) {
        nightlyBuild = isNightly.toBoolean()
    }

    if (project.hasProperty('isPreTest')) {
        preTest = isPreTest.toBoolean()
    }

    if (project.hasProperty('baseImage')) {
        baseImageName = baseImage
    }

    if (project.hasProperty('useDockerDaemon')) {
        useDaemon = useDockerDaemon.toBoolean()
    }

    if (project.hasProperty('multiArchSupport')) {
        multiArch = multiArchSupport.toBoolean()
    }

    if (project.hasProperty('targetPlatform')) {
        targetPlatform = project.property('targetPlatform').toString()
    }

    if (project.hasProperty('workerBaseImageTag')) {
        baseImageTag = workerBaseImageTag
    }
}
