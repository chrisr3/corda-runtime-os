plugins {
    id 'distribution'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.kapt'
    id 'corda.cli-plugin-packager'
    id 'corda.common-publishing'
}

ext {
    releasable = false
}

group 'net.corda.cli.deployment'

dependencies {
    compileOnly "net.corda.cli.host:api:$pluginHostVersion"

    implementation project(":tools:plugins:plugins-rest")
    implementation project(":components:membership:membership-rest")

    implementation libs.jackson.module
    implementation libs.snakeyaml
    implementation libs.pf4j
    kapt libs.pf4j

    testImplementation "net.corda.cli.host:api:$pluginHostVersion"
    testImplementation libs.pf4j
    testImplementation "org.junit.jupiter:junit-jupiter:${junit5Version}"
    testImplementation libs.mockito.core
    testImplementation libs.mockito.kotlin 
    testImplementation "com.github.stefanbirkner:system-lambda:1.2.1"
}

cliPlugin {
    cliPluginClass = 'net.corda.cli.plugins.network.NetworkPluginWrapper'
    cliPluginDescription = 'Plugin for interacting with a network.'
}

tasks.named("installDist") {
    dependsOn cliPlugin
    def homePath = System.properties['user.home']
    from cliPlugin
    into "$homePath/.corda/cli/plugins"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "${cliPlugin.cliPluginId.get()}-cli-plugin"
            groupId project.group
            artifact cliPluginTask
        }
    }
}
